plugins {
    id 'java'
    id 'eclipse'
    id 'application'
    id 'org.openjfx.javafxplugin'      version '0.0.8'
    id 'org.beryx.jlink'               version '2.16.4'
    id 'com.github.ben-manes.versions' version '0.27.0'
}

repositories {
    mavenCentral()
}

mainClassName = 'fxgame/io.game2048.Game2048'

javafx {
    version = '13'
    modules = [
       'javafx.base',
       'javafx.graphics',
       'javafx.controls'
    ]
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    imageZip = project.file("${buildDir}/distributions/game2048-${javafx.platform.classifier}.zip")

    launcher {
        name = 'fxgame'
    }

    jpackage {
        jpackageHome = "/Users/bruno/fxderek/jdk-14.jdk/Contents/Home"
        outputDir = "installer"
        // imageOutputDir = file("$buildDir/my-packaging-image")
        // installerOutputDir = file("${buildDir}/my-packaging-installer")
        imageName = 'fxgame'
        skipInstaller = false
        installerName = 'MyApp'
    }
}

task dist {
    dependsOn clean, jlinkZip
    description "Calls clean and then jlinkZip [default]"
}

defaultTasks 'dist'

eclipse {
    classpath {
        containers 'org.eclipse.buildship.core.gradleclasspathcontainer'
        file {
            whenMerged {
                entries.findAll { it.properties.kind.equals('lib') && !it.properties.path.contains("junit") }.each {
                    it.entryAttributes['module'] = 'true'
                }
                entries.findAll { it.properties.path.startsWith('org.eclipse.jdt.launching.JRE_CONTAINER') }.each {
                    it.entryAttributes['module'] = 'true'
                }

            }
        }
    }
}
