plugins {
    id 'java'
    id 'eclipse'
    id 'application'
    id 'org.openjfx.javafxplugin'      version '0.0.8'
    id 'org.jonnyzzz.java9c'           version '0.2.3'
    id 'org.beryx.jlink'               version '2.15.1'
    id 'com.github.ben-manes.versions' version '0.23.0'
}

repositories {
    mavenCentral()
}

mainClassName = 'fxgame/game2048.Game2048'

javafx {
    version = '12'
    modules = [
       'javafx.base',
       'javafx.graphics',
       'javafx.controls'
    ]
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'game2048'
    }

    imageZip = project.file("${buildDir}/distributions/game2048-${javafx.platform.classifier}.zip")
}

eclipse {
    classpath {
        containers 'org.eclipse.buildship.core.gradleclasspathcontainer'
        file {
            whenMerged {
                entries.findAll { it.properties.kind.equals('lib') && !it.properties.path.contains("junit") }.each {
                    it.entryAttributes['module'] = 'true'
                }
                entries.findAll { it.properties.path.startsWith('org.eclipse.jdt.launching.JRE_CONTAINER') }.each {
                    it.entryAttributes['module'] = 'true'
                }

            }
        }
    }
}
